APPL MMW009 WAIT

INVOKE 'SWS.ESP.APPL.PROCLIB(#DISTRIB)'

/*********************************************************************/
/*02/19/2019 -JFC- CLEANED UP PROC - changed lis and lie jobs
/*********************************************************************/

NOTIFY ABEND FAILURE ALERT(REMD)

FILE_TRIGGER RMS_UPLOAD
   AGENT CYBA_CDC1AKFIS
   FILENAME \\cdc1-ak-fis\Data\CTI-RMS\RMS_UPLOAD.txt +
    CREATE NOCHANGE(3)
   RUN DAILY
   RELEASE ADD(LIS.!ESPAPPL)
ENDJOB

JOB LIS.!ESPAPPL LINK PROCESS
   RUN DAILY
   RELEASE ADD(ZMMLMI09_CD)
ENDJOB

DATA_OBJECT PARMSET.!ESPAPPL
   /*Create a timestamp in hhmmMMDDYY format*/
   SETVAR TMSTMP='!ESPSHH.:!ESPSMN.,!ESPSMM./!ESPSDD./!ESPSYY'
   SETVAR PID=''
   SETVAR STMPTM='!ESPSMM./!ESPSDD./!ESPSYY.,!ESPSHH.:!ESPSMN'
ENDJOB

JOB SETPID.!ESPAPPL LINK PROCESS
   /*AFTER ADD(LISMM.!ESPAPPL) */
   /*get the next PID number from the ConnectDirect*/
   /*Process ID table (CDPROD).  Save the value into*/
   /*the applications DataObject for use by the C:D*/
   /*job and the CDSTAT job (if needed).*/
   VINCR (PID) TABLE(CDPROD) CLANG
   VGET  (PID) TABLE(CDPROD) CLANG
   ESP MGRMSG * . . . PARMSET.!ESPAPPL/!ESPAPPL..!ESPAPGEN/MAIN +
ACTION SET PID(!PID)
   RUN DAILY
ENDJOB

UNIX_JOB ZMMLMI09_CD
   AGENT CYBA_ISBFP3
   AFTER ADD(SETPID.!ESPAPPL)
   EARLYSUB REALNOW PLUS 20 SECONDS
   PID= WOBDATA('PARMSET.!ESPAPPL','PID')
   TMSTMP= WOBDATA('PARMSET.!ESPAPPL','TMSTMP')
   SCRIPTNAME /apps/condir/bfsbin/submitcd.sh
   ARGS zmmlmi09.cd 5 !PID !TMSTMP
   RESOURCE ADD(1,ISBFP3CD)
   USER cdprod
   RUN DAILY
   RELEASE ADD(ZMMLMI09_CONSUMER_TIRE_INFO_RMS)
ENDJOB

SAP_JOB ZMMLMI09_CONSUMER_TIRE_INFO_RMS
   AGENT CYBB_BFP030
   SAPJOBNAME ZMMLMI09_CONSUMER_TIRE_INFO_RMS
   SAPJOBCLASS C
   STARTMODE ASAP
   ABAPNAME ZMMLMI09
   VARIANT DEFAULT
   STEPUSER SAPADM
   LANGUAGE E
   PRINTDEST AK09
   PRINTCOPIES 001
   PRINTREL YES
   EXPIRATION 8
   LINES 65
   COLUMNS 80
   BANNER D
   RECIPIENT SAPADM
   ARCMODE PRINT
   RUN DAILY
   RELEASE ADD(ARZMMLMI09)
ENDJOB

AIX_JOB CDSTAT.ZMMLMI09_CD CONDITIONAL
   AGENT CYBA_ISBFP3
   AFTER ADD(ZMMLMI09_CD(A))
   /*This job only runs if the C:D job does not end with*/
   /*a EXITCODE of 0.  It can be rerun as often as*/
   /*required to check the status of the C:D process.*/
   /*When the C:D process finishes, complete the job*/
   /*to release the successors.*/
   USER cduser
   STMPTM= WOBDATA('PARMSET.!ESPAPPL','STMPTM')
   PID= WOBDATA('PARMSET.!ESPAPPL','PID')
   CMDNAME /apps/condir/bfsbin/cdstat.sh
   ARGS !STMPTM  j!PID
   RUN DAILY
ENDJOB

UNIX_JOB ARZMMLMI09
   AGENT CYBA_ISBFP3
   USER bfpadm
   SCRIPTNAME /local/bin/arcfile.sh
   ARGS interfaces/input/datain/RMS_UPLOAD.txt W
   RUN DAILY
   RELEASE ADD(ZMMLMI09_DELBAT)
ENDJOB

NT_JOB ZMMLMI09_DELBAT
   AGENT CYBA_CDC1AKFIS
   USER maestro
   CMDNAME D:\DTS_RUN\BatchFiles\CTI-RMS\zmmlmi09_delete.bat
   RUN DAILY
   RELEASE ADD(LIE.!ESPAPPL)
ENDJOB

JOB LIE.!ESPAPPL LINK PROCESS
   RUN DAILY
ENDJOB

APPLEND MMW009
   ESPNOMSG TRIGGER !ESPEVENT ADD
ENDJOB

