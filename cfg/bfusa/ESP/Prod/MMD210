APPL MMD210 WAIT

INVOKE 'SWS.ESP.APPL.PROCLIB(#DISTRIB)'

/*********************************************************************/
/*05/09/2018 -JFC- UPDATED PROC AND CHANGED EVENT PREFIX FROM OP01
/*********************************************************************/

NOTIFY FAILURE ABEND ALERT(REMD)
NOTIFY ABEND FAILURE OVERDUE MAILBOX(AJKRONJOBREQ)

DATA_OBJECT PARMSET.!ESPAPPL
   /*Create a timestamp in hhmmMMDDYY format*/
   SETVAR TMSTMP='!ESPSHH.:!ESPSMN.,!ESPSMM./!ESPSDD./!ESPSYY'
   SETVAR PID=''
   SETVAR STMPTM='!ESPSMM./!ESPSDD./!ESPSYY.,!ESPSHH.:!ESPSMN'
ENDJOB

JOB LIS.!ESPAPPL LINK PROCESS
    RUN DAILY
    DELAYSUB 06.03
    RELEASE ADD(ZCOPCR02_NEW_ITEMS_EXT)
ENDJOB

SAP_JOB ZCOPCR02_NEW_ITEMS_EXT
    AGENT CYBB_BFP030
    SAPJOBNAME ZCOPCR02_NEW_ITEMS_EXT
    SAPJOBCLASS C
    SAPUSER SAPADM
    ABAPNAME ZCOPCR02
    VARIANT ORGGPRFORMATSR
    LANGUAGE E
    PRINTDEST CC04
    PRINTCOPIES 001
    EXPIRATION 8
    COLUMNS 132
    BANNER D
    RECIPIENT SAPADM
    ARCMODE PRINT
    RUN DAILY
    RELEASE ADD(SAPTOMFIN_ZCOPCR02CD)
ENDJOB

JOB SETPID.!ESPAPPL LINK PROCESS
   /*get the next PID number from the ConnectDirect*/
   /*Process ID table (CDPROD).  Save the value into*/
   /*the applications DataObject for use by the C:D*/
   /*job and the CDSTAT job (if needed).*/
   VINCR (PID) TABLE(CDPROD) CLANG
   VGET  (PID) TABLE(CDPROD) CLANG
   ESP MGRMSG * . . . PARMSET.!ESPAPPL/!ESPAPPL..!ESPAPGEN/MAIN +
   ACTION SET PID(!PID)
   RUN DAILY
   RELEASE ADD(SAPTOMFIN_ZCOPCR02CD)
ENDJOB

UNIX_JOB SAPTOMFIN_ZCOPCR02CD
    AGENT CYBA_ISBFP3
    USER cdprod
    EARLYSUB REALNOW PLUS 20 SECONDS
    PID= WOBDATA('PARMSET.!ESPAPPL','PID')
    TMSTMP= WOBDATA('PARMSET.!ESPAPPL','TMSTMP')
    SCRIPTNAME  /apps/condir/bfsbin/submitcd.sh
    ARGS saptomfin_zcopcr02.cd 5 !PID !TMSTMP
    RESOURCE ADD(1,ISBFP3CD)
    RUN DAILY
    RELEASE ADD(CDSTAT.ZCOPCR02(A))
    RELEASE ADD(LIE.!ESPAPPL)
ENDJOB

AIX_JOB CDSTAT.ZCOPCR02     CONDITIONAL
   /*This job only runs if the C:D job does not end with*/
   /*a EXITCODE of 0.  It can be rerun as often as*/
   /*required to check the status of the C:D process.*/
   /*When the C:D process finishes, complete the job*/
   /*to release the successors.*/
   AGENT CYBA_ISBFP3
   USER cduser
   STMPTM= WOBDATA('PARMSET.!ESPAPPL','STMPTM')
   PID= WOBDATA('PARMSET.!ESPAPPL','PID')
   CMDNAME /apps/condir/bfsbin/cdstat.sh
   ARGS !STMPTM  j!PID
   RUN DAILY
ENDJOB

JOB LIE.!ESPAPPL LINK PROCESS
    RUN DAILY
ENDJOB

