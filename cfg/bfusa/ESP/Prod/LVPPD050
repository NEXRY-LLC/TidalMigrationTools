APPL LVPPD050 WAIT

INVOKE 'SWS.ESP.APPL.PROCLIB(#DISTRIB)'

/**********************************************************************/
/**INT-162 LAVERGNE Raw Material Consumption                          */
/**BBPRDSAP creates the Raw Material Consumption file                 */
/**F141RAWC (f141rawcon.cd) moves the raw materials file from AS400   */
/**LMMIMI04 (zmmimi04_f141_rm_gi) SAP raw material process            */
/**ARLVRMGI  Archives file F141_RM_GI                                 */
/**********************************************************************/
/*11/14/2016-JFC-MOVED NOTIFY FROM APPL TO JOB LEVEL BECAUSE OF PROB  */
/* WITH AS400 JOB.  ALSO ADDED JOB THAT WILL CANCEL THE AS400 JOB WHEN*/
/* IT FAILS.  THIS WAS DONE WITH THE CONSENT OF GEOFF PURDY.          */
/*ONCE THEY GET THE AS400 JOB FIXED, WE CAN PUT EVERYTHING BACK.      */
/**********************************************************************/
/* 07/23/2018 -JFC- CHANGED ARCHIVE FOLDER FROM W TO D
/* 02/14/2019 -VIC- REMOVED ALERT LV03 PER INC000001852710
/* 09/30/2019 -JFC- REMOVED 4001 CC PARTS per rem INC000002047496
/* 02/24/2020 -JFC- UPDATED AGENT FROM R7 TO R11.5.0
/*                - CHANGED JOBQ FROM CYBESPV210.CYBESPV2100
/* 03/05/2020 -DSG- ADDED RESOURCE TO AS400 JOB - INC000002178363
/* 11/25/2021 -VV - added Mail notification -REQ0098848
/* 04/12/2022 -VV - Added a dummy job for BBPRODSAP - REQ0136407
/* 04/15/2022 -VV - adjusted dummy job - INC0275961
/* 04/21/2022 -VV - adjusted bbprodsap - INC0278050
/* 06/10/2022 -VV - Adjusted BBPRODSAP2 - INC0305145
/**********************************************************************/

NOTIFY FAILURE ABEND ALERT(REMD)

DATA_OBJECT PARMSET.!ESPAPPL
   /*Create a timestamp in hhmmMMDDYY format*/
   SETVAR TMSTMP='!ESPSHH.:!ESPSMN.,!ESPSMM./!ESPSDD./!ESPSYY'
   SETVAR PID=''
   SETVAR STMPTM='!ESPSMM./!ESPSDD./!ESPSYY.,!ESPSHH.:!ESPSMN'
ENDJOB

JOB LIS.!ESPAPPL LINK PROCESS
  RUN DAILY
   IF ESPSHH = '09' THEN DELAYSUB 11.00
   IF ESPSHH = '00' THEN DELAYSUB 02.30
  RELEASE ADD(BBPRODSAP2)
ENDJOB

/*AS400_JOB BBPRODTEST
/*RESOURCE ADD(1,LVPROD)
/*AGENT LVPROD
/*COMMAND CYBROBOT BBPRODTEST
/*JOBQ CAWAAGENT.CYBESPJOBS
/*  RUN DAILY
/*RELEASE ADD(BBPRODSAP.DUMMY(A))
/*RELEASE ADD(BBPRODSAP)
/*ENDJOB

AS400_JOB BBPRODSAP2
  RESOURCE ADD(1,LVPROD)
  AGENT LVPROD
  COMMAND CYBROBOT BBPRODSAP2
  JOBQ CAWAAGENT.CYBESPJOBS
  RUN DAILY
/*RELEASE ADD(BBPRODSAP.DUMMY(A))
  RELEASE ADD(F141RAWCONCD)
ENDJOB

JOB BBPRODSAP.DUMMY LINK PROCESS CONDITIONAL
/*RUN DAILY
ESPNOMSG AJ BBPRODSAP COMPLETE APPL(!ESPAPPL..!ESPAPGEN)
RELEASE ADD(F141RAWCONCD)
ENDJOB

JOB SETPID.!ESPAPPL LINK PROCESS
   /*get the next PID number from the ConnectDirect*/
   /*Process ID table (CDPROD).  Save the value into*/
   /*the applications DataObject for use by the C:D*/
   /*job and the CDSTAT job (if needed).*/
   VINCR (PID) TABLE(CDPROD) CLANG
   VGET  (PID) TABLE(CDPROD) CLANG
   ESP MGRMSG * . . . PARMSET.!ESPAPPL/!ESPAPPL..!ESPAPGEN/MAIN +
   ACTION SET PID(!PID)
   RUN DAILY
   RELEASE ADD(F141RAWCONCD)
ENDJOB

UNIX_JOB F141RAWCONCD
  AGENT CYBA_ISBFP3
  EARLYSUB REALNOW PLUS 20 SECONDS
  PID= WOBDATA('PARMSET.!ESPAPPL','PID')
  TMSTMP= WOBDATA('PARMSET.!ESPAPPL','TMSTMP')
  SCRIPTNAME /apps/condir/bfsbin/submitcd.sh
  ARGS f141rawcon.cd 5 !PID !TMSTMP
  RESOURCE ADD(1,ISBFP3CD)
  USER cdprod
  RUN DAILY
  RELDELAY 2
  RELEASE ADD(CDSTAT.F141RAW(A))
  RELEASE ADD(ZMMIMI04_F141_RM_GI)
ENDJOB

AIX_JOB CDSTAT.F141RAW      CONDITIONAL
   AGENT CYBA_ISBFP3
   /*This job only runs if the C:D job does not end with*/
   /*a EXITCODE of 0.  It can be rerun as often as*/
   /*required to check the status of the C:D process.*/
   /*When the C:D process finishes, complete the job*/
   /*to release the successors.*/
   USER cduser
   STMPTM= WOBDATA('PARMSET.!ESPAPPL','STMPTM')
   PID= WOBDATA('PARMSET.!ESPAPPL','PID')
   CMDNAME /apps/condir/bfsbin/cdstat.sh
   ARGS !STMPTM  j!PID
   RUN DAILY
ENDJOB

SAP_JOB ZMMIMI04_F141_RM_GI
   AGENT CYBB_BFP030
   SAPJOBNAME ZMMIMI04_F141_RM_GI
   SAPUSER SAPADM
   SAPJOBCLASS C
   ABAPNAME ZMMIMI04
   VARIANT 'VARIANT F141'
   LANGUAGE E
   PRINTDEST LV20
   PRINTCOPIES 001
   PRINTIMMED YES
   EXPIRATION 8
   COLUMNS 132
   BANNER YES
   RECIPIENT ATKINWINONA
   ARCMODE PRINT
   NOTIFY FAILURE ABEND MAILBOX(MMJOBNOTIFY)
   NOTIFY FAILURE ABEND ALERT(REMD)
   RUN DAILY
   RELEASE ADD(AR_F141_RM_GI)
ENDJOB

UNIX_JOB AR_F141_RM_GI
  AGENT CYBA_ISBFP3
  SCRIPTNAME /local/bin/arcfile.sh
  ARGS interfaces/input/datain/F141_RM_GI D
  USER bfpadm
  RUN DAILY
  RELEASE ADD(LIE.!ESPAPPL)
ENDJOB

AIX_JOB LIE.!ESPAPPL
  AGENT CYBA_ISBFP3
  COMMAND /bin/sleep
  ARGS 3
  USER bfpadm
  RUN DAILY
  IF ESPSHH = '09' THEN DUEOUT EXEC 12.30
  IF ESPSHH = '00' THEN DUEOUT EXEC 04.30
  NOTIFY OVERDUE MAILBOX(AKRONJOBREQ)
ENDJOB

