APPL LVMMD050 WAIT

INVOKE 'SWS.ESP.APPL.PROCLIB(#DISTRIB)'

/*********************************************************************/
/* 02/24/2020 -JFC- UPDATED AGENT FROM R7 TO R11.5.0                  */
/*                - CHANGED JOBQ FROM CYBESPV210.CYBESPV2100          */
/* 03/05/2020 -DSG- ADDED RESOURCE TO AS400 JOBS - INC000002178363
/*********************************************************************/

NOTIFY FAILURE ABEND ALERT(REMD)

JOB BFPULOCK EXTERNAL APPLID(SUNMAINT) SCHEDULED('YESTERDAY') +
  SCOPE(-04)
  IF ESPSHH = '00' & TODAY('MONDAY') THEN RUN TODAY
  RELEASE ADD(LIS.LVMMD050)
ENDJOB

JOB LIS.LVMMD050 LINK PROCESS
  RUN DAILY
  IF ESPSHH = '21' & TODAY('SUNDAY') THEN NORUN TODAY
  RELEASE ADD(SAPSWCRT.LVPROD)
ENDJOB

DATA_OBJECT PARMSET.!ESPAPPL
  RUN DAILY
  /*Create a timestamp in hhmmMMDDYY format*/
  SETVAR TMSTMP='!ESPSHH.:!ESPSMN.,!ESPSMM./!ESPSDD./!ESPSYY'
  SETVAR PID=''
  SETVAR STMPTM='!ESPSMM./!ESPSDD./!ESPSYY.,!ESPSHH.:!ESPSMN'
ENDJOB

JOB SETPID.!ESPAPPL LINK PROCESS
  /*get the next PID number from the ConnectDirect*/
  /*Process ID table (CDPROD).  Save the value into*/
  /*the applications DataObject for use by the C:D*/
  /*job and the CDSTAT job (if needed).*/
  VINCR (PID) TABLE(CDPROD) CLANG
  VGET  (PID) TABLE(CDPROD) CLANG
  ESP MGRMSG * . . . PARMSET.!ESPAPPL/!ESPAPPL..!ESPAPGEN/MAIN +
  ACTION SET PID(!PID)
  RUN DAILY
  RELEASE ADD(MMD009LVCD)
ENDJOB

AS400_JOB SAPSWCRT.LVPROD
  RESOURCE ADD(1,LVPROD)
  AGENT LVPROD
  COMMAND CYBROBOT SAPSWCRT
  JOBQ CAWAAGENT.CYBESPJOBS
  RUN DAILY
  RELEASE ADD(MMD009LVCD)
ENDJOB

UNIX_JOB MMD009LVCD
  AGENT CYBA_ISBFP3
  EARLYSUB REALNOW PLUS 20 SECONDS
  PID= WOBDATA('PARMSET.!ESPAPPL','PID')
  TMSTMP= WOBDATA('PARMSET.!ESPAPPL','TMSTMP')
  SCRIPTNAME /apps/condir/bfsbin/submitcd.sh
  ARGS mmd009lv.cd 5 !PID !TMSTMP
  RESOURCE ADD(1,ISBFP3CD)
  USER cdprod
  RUN DAILY
  RELEASE ADD(CDSTAT.MMD009CD(A))
  RELEASE ADD(ZMMIMI07_F141)
ENDJOB

AIX_JOB CDSTAT.MMD009CD     CONDITIONAL
   AGENT CYBA_ISBFP3
   /*This job only runs if the C:D job does not end with*/
   /*a EXITCODE of 0.  It can be rerun as often as*/
   /*required to check the status of the C:D process.*/
   /*When the C:D process finishes, complete the job*/
   /*to release the successors.*/
   USER cduser
   STMPTM= WOBDATA('PARMSET.!ESPAPPL','STMPTM')
   PID= WOBDATA('PARMSET.!ESPAPPL','PID')
   CMDNAME /apps/condir/bfsbin/cdstat.sh
   ARGS !STMPTM  j!PID
   RUN DAILY
ENDJOB

SAP_JOB ZMMIMI07_F141
  AGENT CYBB_BFP030
  SAPJOBNAME ZMMIMI07_F141
  SAPUSER SAPADM
  SAPJOBCLASS C
  ABAPNAME ZMMIMI07
  VARIANT F141
  LANGUAGE E
  PRINTDEST LV17
  PRINTCOPIES 001
  PRINTIMMED YES
  EXPIRATION 8
  COLUMNS 132
  BANNER D
  RECIPIENT STORES
  ARCMODE PRINT
  RUN DAILY
  RELEASE ADD(ARZMMIMI07_IID_F141)
ENDJOB

UNIX_JOB ARZMMIMI07_IID_F141
  AGENT CYBA_ISBFP3
  SCRIPTNAME /local/bin/arcfile.sh
  ARGS interfaces/input/datain/zmmimi07_iid_f141 D
  USER bfpadm
  RUN DAILY
  RELEASE ADD(SAPSWCLR.LVPROD)
ENDJOB

AS400_JOB SAPSWCLR.LVPROD
  RESOURCE ADD(1,LVPROD)
  AGENT LVPROD
  COMMAND CYBROBOT SAPSWCLR
  JOBQ CAWAAGENT.CYBESPJOBS
  RUN DAILY
  RELEASE ADD(LIE.LVMMD050)
ENDJOB

JOB LIE.LVMMD050 LINK PROCESS
  RUN DAILY
ENDJOB

