APPL PED120AG WAIT

INVOKE 'SWS.ESP.APPL.PROCLIB(#DISTRIB)'

/*********************************************************************/
/*THE MIMI CD JOBS IN THIS APPL COMMUNICATE WITH APPM-NA-CHQ         */
/*********************************************************************/
/*03/27/2019 -JFC- changes made per INC000001858289                  */
/*********************************************************************/

NOTIFY FAILURE ABEND ALERT(REMD)

JOB LIS.!ESPAPPL LINK PROCESS
  RUN DAILY
  DELAYSUB 22.20
  RELEASE ADD(YMIMCOO_MIMI_MMP_COO_EXT_AG)
ENDJOB

JOB BFPULOCK EXTERNAL APPLID(SUNMAINT) SCOPE(-04)
  RUN SUN
  RELEASE ADD(YMIMCOO_MIMI_MMP_COO_EXT_AG)
ENDJOB

DATA_OBJECT PARMSET.!ESPAPPL
   /*Create a timestamp in hhmmMMDDYY format*/
   SETVAR TMSTMP='!ESPSHH.:!ESPSMN.,!ESPSMM./!ESPSDD./!ESPSYY'
   SETVAR PID=''
   SETVAR STMPTM='!ESPSMM./!ESPSDD./!ESPSYY.,!ESPSHH.:!ESPSMN'
ENDJOB

JOB SETPID.!ESPAPPL LINK PROCESS
   /*get the next PID number from the ConnectDirect*/
   /*Process ID table (CDPROD).  Save the value into*/
   /*the applications DataObject for use by the C:D*/
   /*job and the CDSTAT job (if needed).*/
   VINCR (PID) TABLE(CDPROD) CLANG
   VGET  (PID) TABLE(CDPROD) CLANG
   ESP MGRMSG * . . . PARMSET.!ESPAPPL/!ESPAPPL..!ESPAPGEN/MAIN +
   ACTION SET PID(!PID)
   RUN DAILY
   RELEASE ADD(SAPTOMIMI_MATCOO_AGCD)
ENDJOB

SAP_JOB YMIMCOO_MIMI_MMP_COO_EXT_AG
    SAPUSER SAPADM
    AGENT CYBB_BFP030
    SAPJOBNAME 'YMIMCOO_MIMI MMP COO EXT AG'
    SAPJOBCLASS C
    ABAPNAME YMIMCOO
    VARIANT AG
    LANGUAGE E
    PRINTDEST LOCL
    PRINTCOPIES 001
    EXPIRATION 8
    LINES 65
    COLUMNS 80
    BANNER D
    RECIPIENT SAPADM
    ARCMODE PRINT
    RUN DAILY
    RELEASE ADD(SAPTOMIMI_MATCOO_AGCD)
ENDJOB

UNIX_JOB SAPTOMIMI_MATCOO_AGCD
    AGENT CYBA_ISBFP3
    USER cdprod
    RESOURCE ADD(1,ISBFP3CD)
    EARLYSUB REALNOW PLUS 20 SECONDS
    PID= WOBDATA('PARMSET.!ESPAPPL','PID')
    TMSTMP= WOBDATA('PARMSET.!ESPAPPL','TMSTMP')
    SCRIPTNAME /apps/condir/bfsbin/submitcd.sh
    ARGS saptomimi_matcoo_ag.cd 7 !PID !TMSTMP
    RUN DAILY
    RELEASE ADD(LIE.!ESPAPPL)
    RELEASE ADD(CDSTAT.AGMATCOO(A))
ENDJOB

AIX_JOB CDSTAT.AGMATCOO    CONDITIONAL
   AGENT CYBA_ISBFP3
   /*This job only runs if the C:D job does not end with*/
   /*a EXITCODE of 0.  It can be rerun as often as*/
   /*required to check the status of the C:D process.*/
   /*When the C:D process finishes, complete the job*/
   /*to release the successors.*/
   USER cduser
   STMPTM= WOBDATA('PARMSET.!ESPAPPL','STMPTM')
   PID= WOBDATA('PARMSET.!ESPAPPL','PID')
   CMDNAME /apps/condir/bfsbin/cdstat.sh
   ARGS !STMPTM  j!PID
   RUN DAILY
ENDJOB

AIX_JOB LIE.!ESPAPPL
   AGENT CYBA_ISBFP3
   COMMAND /bin/sleep
   ARGS 3
   USER bfpadm
   RUN DAILY
ENDJOB

