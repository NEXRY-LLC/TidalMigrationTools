APPL APM010 WAIT

INVOKE 'SWS.ESP.APPL.PROCLIB(#DISTRIB)'

/*********************************************************************/
/*Joliette AP Check Extract USD
/*APM010  Creates AP extract-u file
/*APM010CD Transfers AP extract file fron SAP to AS400
/*JOF201CHQU  Reformats file to Joliette's legacy system.
/*********************************************************************/
/* 03/18/2019 -JFC-  UPDATED PROC PER  INC000001878221                */
/* 02/24/2020 -JFC- UPDATED AGENT FROM R7 TO R11.5.0                  */
/*                - CHANGED JOBQ FROM CYBESPV210.CYBESPV2100          */
/* 03/05/2020 -DSG- ADDED RESOURCE TO AS400 JOB - INC000002178387
/*********************************************************************/

NOTIFY FAILURE ABEND ALERT(REMD)

DATA_OBJECT PARMSET.!ESPAPPL
   /*Create a timestamp in hhmmMMDDYY format*/
   SETVAR TMSTMP='!ESPSHH.:!ESPSMN.,!ESPSMM./!ESPSDD./!ESPSYY'
   SETVAR PID=''
   SETVAR STMPTM='!ESPSMM./!ESPSDD./!ESPSYY.,!ESPSHH.:!ESPSMN'
ENDJOB

JOB LIS.!ESPAPPL LINK PROCESS
   RUN 2ND WORKDAY OF MONTH
   DELAYSUB 13.05
   RELEASE ADD(RFCHKE00_JOLIETTE_AP_EXTRACT_USD)
ENDJOB

JOB SETPID.APM010  LINK PROCESS
   RUN 2ND WORKDAY OF MONTH
   /*get the next PID number from the ConnectDirect*/
   /*Process ID table (CDPROD).  Save the value into*/
   /*the applications DataObject for use by the C:D*/
   /*job and the CDSTAT job (if needed).*/
   VINCR (PID) TABLE(CDPROD) CLANG
   VGET  (PID) TABLE(CDPROD) CLANG
   ESP MGRMSG * . . . PARMSET.!ESPAPPL/!ESPAPPL..!ESPAPGEN/MAIN +
ACTION SET PID(!PID)
   RELEASE ADD(APM010CD.ISBFP3)
ENDJOB

SAP_JOB RFCHKE00_JOLIETTE_AP_EXTRACT_USD
   SAPUSER SAPADM
   AGENT CYBB_BFP030
   SAPJOBNAME 'RFCHKE00_JOLIETTE AP EXTRACT USD'
   SAPJOBCLASS A
   ABAPNAME RFCHKE00
   VARIANT JOLIETTE-USD
   LANGUAGE E
   PRINTDEST JL01
   PRINTCOPIES 001
   EXPIRATION 8
   LINES 65
   COLUMNS 132
   BANNER D
   RECIPIENT LADOUCEURLUC
   ARCMODE PRINT
   ABAPNAME ZFIAPI00
   VARIANT JOLIETTE-USD
   LANGUAGE E
   PRINTDEST JL01
   PRINTCOPIES 001
   EXPIRATION 8
   COLUMNS 132
   BANNER D
   RECIPIENT LADOUCEURLUC
   ARCMODE PRINT
   RUN 2ND WORKDAY OF MONTH
   RELEASE ADD(APM010CD.ISBFP3)
ENDJOB

UNIX_JOB APM010CD.ISBFP3
   AGENT CYBA_ISBFP3
   EARLYSUB REALNOW PLUS 20 SECONDS
   PID= WOBDATA('PARMSET.!ESPAPPL','PID')
   TMSTMP= WOBDATA('PARMSET.!ESPAPPL','TMSTMP')
   SCRIPTNAME /apps/condir/bfsbin/submitcd.sh
   ARGS apm010cd.cd 5 !PID !TMSTMP
   RUN 2ND WORKDAY OF MONTH
   RESOURCE ADD(1,ISBFP3CD)
   USER cdprod
   RELEASE ADD(CDSTAT.APM010CD(A))
   RELEASE ADD(JOF201CHQU)
ENDJOB

AIX_JOB CDSTAT.APM010CD CONDITIONAL
   /*This job only runs if the C:D job does not end with*/
   /*a EXITCODE of 0.  It can be rerun as often as*/
   /*required to check the status of the C:D process.*/
   /*When the C:D process finishes, complete the job*/
   /*to release the successors.*/
   AGENT CYBA_ISBFP3
   USER cduser
   STMPTM= WOBDATA('PARMSET.!ESPAPPL','STMPTM')
   PID= WOBDATA('PARMSET.!ESPAPPL','PID')
   CMDNAME /apps/condir/bfsbin/cdstat.sh
   ARGS !STMPTM  j!PID
   RUN 2ND WORKDAY OF MONTH
ENDJOB

AS400_JOB JOF201CHQU
  RESOURCE ADD(1,AKNITS)
   AGENT AKNITS
   COMMAND CYBROBOT JOF201CHQU
   JOBQ CAWAAGENT.CYBESPJOBS
   RUN 2ND WORKDAY OF MONTH
   RELEASE ADD(LIE.!ESPAPPL)
ENDJOB

JOB LIE.!ESPAPPL LINK PROCESS
   RUN 2ND WORKDAY OF MONTH
ENDJOB

