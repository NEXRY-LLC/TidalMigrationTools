APPL PED120PS WAIT

NOTIFY FAILURE ABEND ALERT(REMD)

INVOKE 'SWS.ESP.APPL.PROCLIB(#DISTRIB)'

/*********************************************************************/
/*THE MIMI CD JOBS IN THIS APPL COMMUNICATE WITH APPM-NA-CHQ         */
/*********************************************************************/

AIX_JOB LIE.PED120OR EXTERNAL APPLID(PED120OR)
  RUN DAILY
  RELEASE ADD(LIS.!ESPAPPL)
ENDJOB

JOB LIS.!ESPAPPL LINK PROCESS
  RUN DAILY
  DELAYSUB 22.15
  RELEASE ADD(YMIMCOO_MMIMI_MMP_COO_EXT_PS)
ENDJOB

SAP_JOB YMIMCOO_MMIMI_MMP_COO_EXT_PS                                    _PS')
    SAPUSER SAPADM
    AGENT CYBB_BFP030
    SAPJOBNAME 'YMIMCOO_MIMI MMP COO EXT PS'
    SAPJOBCLASS C
    ABAPNAME YMIMCOO
    VARIANT PASS/LT
    LANGUAGE E
    PRINTCOPIES 001
    EXPIRATION 8
    LINES 65
    COLUMNS 80
    BANNER D
    RECIPIENT MADDUXGEORGE
    ARCMODE PRINT
    RUN DAILY
    RELEASE ADD(SAPTOMIMI_MATCOO_PSCD)
ENDJOB

DATA_OBJECT PARMSET.!ESPAPPL
   /*Create a timestamp in hhmmMMDDYY format*/
   SETVAR TMSTMP='!ESPSHH.:!ESPSMN.,!ESPSMM./!ESPSDD./!ESPSYY'
   SETVAR PID=''
   SETVAR STMPTM='!ESPSMM./!ESPSDD./!ESPSYY.,!ESPSHH.:!ESPSMN'
ENDJOB

JOB SETPID.!ESPAPPL LINK PROCESS
   /*get the next PID number from the ConnectDirect*/
   /*Process ID table (CDPROD).  Save the value into*/
   /*the applications DataObject for use by the C:D*/
   /*job and the CDSTAT job (if needed).*/
   VINCR (PID) TABLE(CDPROD) CLANG
   VGET  (PID) TABLE(CDPROD) CLANG
   ESP MGRMSG * . . . PARMSET.!ESPAPPL/!ESPAPPL..!ESPAPGEN/MAIN +
   ACTION SET PID(!PID)
   RUN DAILY
   RELEASE ADD(SAPTOMIMI_MATCOO_PSCD)
ENDJOB

UNIX_JOB SAPTOMIMI_MATCOO_PSCD
    AGENT CYBA_ISBFP3
    USER cdprod
    EARLYSUB REALNOW PLUS 20 SECONDS
    PID= WOBDATA('PARMSET.!ESPAPPL','PID')
    TMSTMP= WOBDATA('PARMSET.!ESPAPPL','TMSTMP')
    SCRIPTNAME /apps/condir/bfsbin/submitcd.sh
    ARGS saptomimi_matcoo_ps.cd 5 !PID !TMSTMP
    RUN DAILY
    RELEASE ADD(CDSTAT.PSMATCOO(A))
    RELEASE ADD(LIE.!ESPAPPL)
ENDJOB

AIX_JOB CDSTAT.PSMATCOO     CONDITIONAL
   AGENT CYBA_ISBFP3
   /*This job only runs if the C:D job does not end with*/
   /*a EXITCODE of 0.  It can be rerun as often as*/
   /*required to check the status of the C:D process.*/
   /*When the C:D process finishes, complete the job*/
   /*to release the successors.*/
   USER cduser
   STMPTM= WOBDATA('PARMSET.!ESPAPPL','STMPTM')
   PID= WOBDATA('PARMSET.!ESPAPPL','PID')
   CMDNAME /apps/condir/bfsbin/cdstat.sh
   ARGS !STMPTM  j!PID
   RUN DAILY
ENDJOB

AIX_JOB LIE.!ESPAPPL
   AGENT CYBA_ISBFP3
   COMMAND /bin/sleep
   ARGS 3
   USER bfpadm
   RUN DAILY
ENDJOB

