APPL DI203C WAIT

INVOKE 'SWS.ESP.APPL.PROCLIB(#DISTRIB)'

/*********************************************************************/
/* 03/17/2016 - JFC - UPDATING DOCUMENTATION AND STANDARDS           */
/* 03/22/2017 - JFC - MAJOR OVERHAUL - OLD VERSION SAVED AS DI203CPY */
/* 07/13/2018 - DSG - UPDATED EXTERNALS AFTER DRP CONVERSION         */
/*********************************************************************/

NOTIFY FAILURE ABEND ALERT(REMD)
NOTIFY OVERDUE MAILBOX(WPS2DRP)

/*********************************************************************/
/* DATASET TRIGGERS BEGIN                                            */
/*********************************************************************/

DSTRIG DISTO.FROMSAP
  /*DSN CREATED IN APPL MMD330*/
  DSNAME 'DI.PRD.STO.FROMSAP.G-'
  RUN WEEKDAYS SAT
  RELEASE ADD(DI700A)
ENDJOB

DSTRIG DICUSTORD.FROMSAP
  /*DSN CREATED IN APPL LED003*/
  DSNAME 'DI.PRD.CUSTORD.FROMSAP.G-'
  RUN WEEKDAYS SAT
  RELEASE ADD(DI700A)
ENDJOB

DSTRIG DIPO.FROMSAP
  /*DSN CREATED IN APPL MMD335*/
  DSNAME 'DI.PRD.PO.FROMSAP.G-'
  RUN WEEKDAYS SAT
  RELEASE ADD(DI700A)
ENDJOB

DSTRIG DIINV.FROMSAP
  /*CREATED IN APPL MMD410*/
  DSNAME 'DI.PRD.INV.FROMSAP.G-'
  RUN WEEKDAYS SAT
  RELEASE ADD(DI200A)
ENDJOB

DSTRIG DISAGREE.FROMSAP
  DSNAME 'DI.PRD.SCHEDLNG.AGREEMNT.FROMSAP.G-'
    /*CREATED IN APPL LED004*/
  RUN WEEKDAYS SAT
  RELEASE ADD(DI200A)
ENDJOB

DSTRIG MTDSHIP.FROMSAP
   /*DATASET  CREATED IN SSISMISC - INTERFACE_1128 */
  DSNAME 'PS.PRD.MTD.SHIPMNTS.FROMSAP.G-'
  NOTIFY OVERDUE MAILBOX(WPS2DRP)
  DUEOUT EXEC 8AM
  RELEASE ADD(PS210A)
ENDJOB

/*********************************************************************/
/*EXTERNAL JOB DEFINITIONS BEGIN                                     */
/*********************************************************************/

LINUX_JOB LIE.MMD330 EXTERNAL APPLID(MMD330)
  /* REPLACING ARZMMIMI37_STO_DRP - 07/13/18 - DSG                   */
  RUN WEEKDAYS SAT
  RELEASE ADD(DI700A)
ENDJOB

LINUX_JOB LIE.MMD335 EXTERNAL APPLID(MMD335)
  /* REPLACING ARZMMPUI01_PO_DRP - 07/13/18 - DSG                   */
  RUN WEEKDAYS SAT
  RELEASE ADD(DI700A)
ENDJOB

LINUX_JOB LIE.MMD410 EXTERNAL APPLID(MMD410)
  /* REPLACING ARZMMIMI44_ON_HAND_INV - 07/13/18 - DSG                */
  RUN WEEKDAYS SAT
  RELEASE ADD(DI200A)
ENDJOB

LINUX_JOB LIE.LED003 EXTERNAL APPLID(LED003)
  /* REPLACING ARZSDLEI07 - 07/13/18 - DSG                */
  RUN WEEKDAYS SAT
  RELEASE ADD(DI700A)
ENDJOB

LINUX_JOB LIE.LED004 EXTERNAL APPLID(LED004)
  /* REPLACING ARZSDLEI12 - 07/13/18 - DSG                */
  RUN WEEKDAYS SAT
  RELEASE ADD(DI200A)
ENDJOB

JOB DI203E EXTERNAL SCOPE(-07)
  RUN 1ST DAY OF MONTH
  RELEASE ADD(DI203C)
ENDJOB

/* JOB PS210A EXTERNAL SCOPE(-04)
/*   RUN WEEKDAYS SAT
/*   IF TODAY('SUN') & TODAY('1ST DAY OF MONTH') THEN +
/*   RUN TODAY
/*   RELEASE ADD(DI203C)
/* ENDJOB

/**********************************************************************/
/*  JOB DEFINITIONS BEGIN                                             */
/**********************************************************************/

JOB DI700A
  /*@@ SYSTEM=DRP
  CCCHK RC(2:4095) FAIL CONTINUE
  RUN WEEKDAYS SAT
  DELAYSUB 07:00
  RELEASE ADD(DI200A)
ENDJOB

JOB DI200A
  CCCHK RC(1:4095) FAIL CONTINUE
  RUN WEEKDAYS SAT
  RELEASE ADD(PS200A)
ENDJOB

JOB PS200A
  /*@@ SYSTEM=MPS
  CCCHK RC(4:4095) FAIL CONTINUE
  RUN 1ST DAY OF MONTH
  RUN WEEKDAYS SAT
  RELEASE ADD(PS210A)
ENDJOB

JOB DI203C
  /*@@ SYSTEM=DRP
  CCCHK RC(1:4095) FAIL CONTINUE
  RUN WEEKDAYS SAT
  IF TODAY('SUN') & TODAY('1ST DAY OF MONTH') THEN +
    RUN TODAY
  RELEASE ADD(DI203B)
ENDJOB

JOB PS210A
  CCCHK RC(4:4095) FAIL CONTINUE
  RUN 1ST DAY OF MONTH
  RUN WEEKDAYS SAT
  RELEASE ADD(DI203C)
ENDJOB

JOB DI203B
  /*@@ SYSTEM=DRP
  CCCHK RC(1:4095) FAIL CONTINUE
  RUN WEEKDAYS SAT
  IF TODAY('SUN') & TODAY('1ST DAY OF MONTH') THEN +
    RUN TODAY
  RELEASE ADD(DI203B01)
ENDJOB

JOB DI203B01
  /*@@ SYSTEM=DRP
  CCCHK RC(1:4095) FAIL CONTINUE
  RUN 1ST DAY OF MONTH
ENDJOB

