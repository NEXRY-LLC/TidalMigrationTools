APPL WNMIMI03 WAIT

INVOKE 'SWS.ESP.APPL.PROCLIB(#DISTRIB)'

/*********************************************************************/
/* 05/09/2018 -JFC- CREATED NEW APPLICATION TO REPLACE MMD003
/* 02/25/2020 -DSG- UPDATED AGENT FROM R7 TO R11.5.0
/*                - CHANGED JOBQ FROM CYBESPV210.CYBESPV2100
/* 03/05/2020 -DSG- ADDED RESOURCE TO AS400 JOB - INC000002178397
/*********************************************************************/

NOTIFY FAILURE ABEND ALERT(REMD)
NOTIFY ABEND FAILURE OVERDUE MAILBOX(AJKRONJOBREQ)

DATA_OBJECT PARMSET.!ESPAPPL
   /*Create a timestamp in hhmmMMDDYY format*/
   SETVAR TMSTMP='!ESPSHH.:!ESPSMN.,!ESPSMM./!ESPSDD./!ESPSYY'
   SETVAR PID=''
   SETVAR STMPTM='!ESPSMM./!ESPSDD./!ESPSYY.,!ESPSHH.:!ESPSMN'
ENDJOB

JOB LIS.!ESPAPPL LINK PROCESS
   RUN DAILY
   EARLYSUB 01.45
   RELEASE ADD(ZMMIMI03_F161)
ENDJOB

SAP_JOB ZMMIMI03_F161
   AGENT CYBB_BFP030
   SAPUSER SAPADM
   SAPJOBNAME ZMMIMI03_F161
   SAPJOBCLASS C
   ABAPNAME ZMMIMI03
   VARIANT ZMMIMI03_F161
   LANGUAGE E
   PRINTDEST WN41
   ARCMODE PRINT
   RECIPIENT WITHERSMIKE
   COLUMNS 132
   EXPIRATION 8
   PRINTCOPIES 001
   STARTMODE ASAP
   RUN DAILY
   RELEASE ADD(MMD003CD.ISBFP3)
ENDJOB

JOB SETPID.!ESPAPPL LINK PROCESS
   /*get the next PID number from the ConnectDirect*/
   /*Process ID table (CDPROD).  Save the value into*/
   /*the applications DataObject for use by the C:D*/
   /*job and the CDSTAT job (if needed).*/
   VINCR (PID) TABLE(CDPROD) CLANG
   VGET  (PID) TABLE(CDPROD) CLANG
   ESP MGRMSG * . . . PARMSET.!ESPAPPL/!ESPAPPL..!ESPAPGEN/MAIN +
   ACTION SET PID(!PID)
   RUN DAILY
   RELEASE ADD(MMD003CD.ISBFP3)
ENDJOB

UNIX_JOB MMD003CD.ISBFP3
   /*CONNECT:DIRECT Script to move raw materials file
   /*zmmimi03_f161_roh.dat from ISBFP3 to Warren Plant AS/400
   AGENT CYBA_ISBFP3
   USER cdprod
   EARLYSUB REALNOW PLUS 20 SECONDS
   PID= WOBDATA('PARMSET.!ESPAPPL','PID')
   TMSTMP= WOBDATA('PARMSET.!ESPAPPL','TMSTMP')
   SCRIPTNAME /apps/condir/bfsbin/submitcd.sh
   ARGS mmd003cd.cd 5 !PID !TMSTMP
   RESOURCE ADD(1,ISBFP3CD)
   RUN DAILY
   RELEASE ADD(CDSTAT.MMD003CD(A))
   RELEASE ADD(TBL7066.WRNPROD)
ENDJOB

AIX_JOB CDSTAT.MMD003CD CONDITIONAL
   /*This job only runs if the C:D job does not end with*/
   /*a EXITCODE of 0.  It can be rerun as often as*/
   /*required to check the status of the C:D process.*/
   /*When the C:D process finishes, complete the job*/
   /*to release the successors.*/
   AGENT CYBA_ISBFP3
   USER cduser
   STMPTM= WOBDATA('PARMSET.!ESPAPPL','STMPTM')
   PID= WOBDATA('PARMSET.!ESPAPPL','PID')
   CMDNAME /apps/condir/bfsbin/cdstat.sh
   ARGS !STMPTM  j!PID
   RUN DAILY
ENDJOB

AS400_JOB TBL7066.WRNPROD
   RESOURCE ADD(1,WRNPROD)
   AGENT WRNPROD
   COMMAND CYBROBOT TBL7066
   JOBQ CAWAAGENT.CYBESPJOBS
   RUN DAILY
   RELEASE ADD(LIE.!ESPAPPL)
ENDJOB

JOB LIE.!ESPAPPL LINK PROCESS
   RUN DAILY
ENDJOB

