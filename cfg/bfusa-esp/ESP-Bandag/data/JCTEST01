APPL JCTEST01 WAIT

INVOKE 'BANDAG.ESP.APPL.PROCLIB(#DISTRIB)'

/**********************************************************************/
/*12/14/2016 -JFC- NEW APPL FOR TESTING STUFF                         */
/**********************************************************************/
/*LET'S FIND OUT WHAT INPUT DATES WE NEED AND ASSIGN THEM TO A VARIABLE
/*  TODAYS DATE WORKDAY = TDWD2
/*  TODAY PLUS 2 WORKDAYS = TDP2WD2 (INCLUDE HOLIDAYS IN COUNT=WEEKDAYS)
/*  TODAY PLUS 5 WORKDAYS = TDP5WD2 (INCLUDE HOLIDAYS = WEEKDAYS)
/*  TODAY LESS 5 WORKDAYS = TDL5WD2 (**EXCLUDE HOLIDAYS = WORKDAYS**)
/*  LAST WORKDAY = TDL1WD2
/*  DAY OF WEEK = TDDOW2 (THIS IS A NUMBER FROM 1-5 REPRESENTING M-F)
/*  FIRST WORKDAY OF WEEK = FWDOW2 (THIS IS A Y/N FIELD)
/*  PROD EST FLAG = PEF2 (ALWAYS ZERO FOR 319 - 1 FOR JOB 339)
/*  FIRST WORKDAY OF MONTH = FWDOM2 (THIS IS A Y/N FIELD)
/**********************************************************************/
/* CREATE GENTIMES FOR BOPP0319 INPUT VALUES
GENTIME TDWD TODAY
GENTIME TDP2WD TODAY PLUS 2 WEEKDAYS
GENTIME TDP5WD TODAY PLUS 5 WEEKDAYS
GENTIME TDL5WD TODAY LESS 5 WORKDAYS
GENTIME TDL1WD TODAY LESS 1 WORKDAY
GENTIME TDDOW TODAY
GENTIME FWDOW FIRST WORKDAY OF WEEK STARTING TODAY
GENTIME FWDOM FIRST WORKDAY OF MONTH STARTING TODAY
/* CREATE ADDITIONAL GENTIMES FOR BOPP0399 INPUT VALUES
GENTIME TDP2WK TODAY PLUS 2 WORKDAYS
GENTIME TDP5WK TODAY PLUS 5 WORKDAYS
/**********************************************************************/
/* CREATE THE VARIABLES AND ASSIGN THEM VALUES
TDWD1 = '!TDWDDATE'
TDWD2 = '!TDWDYEAR!TDWDMM!TDWDDD'
TDP2WD2 = '!TDP2WDYEAR!TDP2WDMM!TDP2WDDD'
TDP2WD3 = '!TDP2WKYEAR!TDP2WKMM!TDP2WKDD'
TDP5WD2 = '!TDP5WDYEAR!TDP5WDMM!TDP5WDDD'
TDP5WD3 = '!TDP5WKYEAR!TDP5WKMM!TDP5WKDD'
TDL5WD2 = '!TDL5WDYEAR!TDL5WDMM!TDL5WDDD'
TDL1WD2 = '!TDL1WDYEAR!TDL1WDMM!TDL1WDDD'
TDDOW2 = '!TDWDDOW#' - 1
FWDOW2 = 'N'
 FWDOW1 = '!FWDOWDATE'
  IF '!TDWDDATE' = '!FWDOW1' THEN FWDOW2 = 'Y'
FWDOM2 = 'N'
 FWDOM1 = '!FWDOMDATE'
  IF '!TDWDDATE' = '!FWDOM1' THEN FWDOM2 = 'Y'
PEF1 = '1'
PEF2 = '0'
/**********************************************************************/
/*NEED TO PASS THESE VARIABLES TO THE JOB
/*USER5 WILL BE FOR JOB BOPP0319
DATEPARMS5 = '!TDWD2 !TDP2WD2 !TDP5WD2 !TDL5WD2 !TDL1WD2 !TDDOW2 +
!FWDOW2 !PEF2 !FWDOM2'
USER5 = !DATEPARMS5
/*USER6 WILL BE FOR JOB BOPP0399
DATEPARMS6 = '!TDWD2 !TDP2WD3 !TDP5WD3 !TDL5WD2 !TDL1WD2 !TDDOW2 +
!FWDOW2 !PEF1 !FWDOM2'
USER6 = !DATEPARMS6
/**********************************************************************/

JOB JCTST319.1 LINK PROCESS
  ECHO !USER5
  RUN WEEKDAYS
  NORUN HOLIDAYS
  RELEASE ADD(JCTEMAIL.319)
  RELEASE ADD(BOPPT319.319)
ENDJOB

JOB JCTST399.2 LINK PROCESS
  ECHO !USER6
  RUN HOLIDAY LESS 1 WORKDAY
  RUN HOLIDAY LESS 2 WORKDAY
  RELEASE ADD(IEBGENJC.399)
ENDJOB

JOB BOPPT319.319 HOLD
  RUN WEEKDAYS
  NORUN HOLIDAYS
ENDJOB

JOB JCTEMAIL.319 HOLD
  RUN WEEKDAYS
  NORUN HOLIDAYS
ENDJOB

JOB JCPP0013 LINK PROCESS
  ECHO '!TDWD2' = TODAYS DATE
  ECHO '!TDP2WD2'= TODAY PLUS 2 WORKDAYS
  ECHO '!TDP5WD2'= TODAY PLUS 5 WORKDAYS
  ECHO '!TDL5WD2'= TODAY LESS 5 WORKDAYS
  ECHO '!TDL1WD2'= LAST WORKDAY
  ECHO '!TDDOW2' = DAY OF WEEK
  ECHO '!FWDOW2' = FIRST WORKDAY OF WEEK?
  ECHO '!PEF2' = PROD EST FLAG
  ECHO '!FWDOM2' = FIRST WORKDAY OF MONTH?
ENDJOB

/**********************************************************************/

