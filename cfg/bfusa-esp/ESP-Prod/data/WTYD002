APPL WTYD002 WAIT

INVOKE 'SWS.ESP.APPL.PROCLIB(#DISTRIB)'

/*********************************************************************/
/*05/09/2018 -JFC- UPDATED PROC AND CHANGED EVENT PREFIX FROM OP01
/*********************************************************************/

NOTIFY ABEND FAILURE ALERT(REMD)

DATA_OBJECT PARMSET.!ESPAPPL
   /*Create a timestamp in hhmmMMDDYY format*/
   SETVAR TMSTMP='!ESPSHH.:!ESPSMN.,!ESPSMM./!ESPSDD./!ESPSYY'
   SETVAR PID=''
   SETVAR STMPTM='!ESPSMM./!ESPSDD./!ESPSYY.,!ESPSHH.:!ESPSMN'
ENDJOB

JOB LIS.!ESPAPPL LINK PROCESS
   RUN WEEKDAYS
   EARLYSUB 04.00
   RELEASE ADD(ZSDWTYI03_CLAIMS_TO_JP)
ENDJOB

SAP_JOB ZSDWTYI03_CLAIMS_TO_JP
   AGENT CYBB_BFP030
   SAPJOBNAME ZSDWTYI03_CLAIMS_TO_JP
   SAPJOBCLASS C
   STARTMODE ASAP
   ABAPNAME ZSDWTYI03
   VARIANT DAILY_EXTRACT
   STEPUSER SAPADM
   LANGUAGE E
   PRINTDEST LP01
   EXPIRATION 8
   COLUMNS 255
   BANNER D
   RECIPIENT ZFF_OTC_CO20
   ARCMODE PRINT
   PRINTCOPIES 001
   RUN WEEKDAYS
   RELEASE ADD(ZSDWTYI03_CLMS_TO_JP)
ENDJOB

JOB SETPID.!ESPAPPL LINK PROCESS
   /*get the next PID number from the ConnectDirect*/
   /*Process ID table (CDPROD).  Save the value into*/
   /*the applications DataObject for use by the C:D*/
   /*job and the CDSTAT job (if needed).*/
   VINCR (PID) TABLE(CDPROD) CLANG
   VGET  (PID) TABLE(CDPROD) CLANG
   ESP MGRMSG * . . . PARMSET.!ESPAPPL/!ESPAPPL..!ESPAPGEN/MAIN +
ACTION SET PID(!PID)
   RUN WEEKDAYS
   RELEASE ADD(ZSDWTYI03_CLMS_TO_JP)
ENDJOB

UNIX_JOB ZSDWTYI03_CLMS_TO_JP
   AGENT CYBA_ISBFP3
   USER cdprod
   EARLYSUB REALNOW PLUS 20 SECONDS
   PID= WOBDATA('PARMSET.!ESPAPPL','PID')
   TMSTMP= WOBDATA('PARMSET.!ESPAPPL','TMSTMP')
   SCRIPTNAME /apps/condir/bfsbin/submitcd.sh
   ARGS zsdwtyi03.cd 5 !PID !TMSTMP
   RESOURCE ADD(1,ISBFP3CD)
   RUN WEEKDAYS
   RELEASE ADD(CDSTAT.ZSDWTYI03_CLMS_TO_JP(A))
   RELEASE ADD(ARZSDWTYI03_CLMS_TO_JP)
ENDJOB

AIX_JOB CDSTAT.ZSDWTYI03_CLMS_TO_JP CONDITIONAL
   /*This job only runs if the C:D job does not end with*/
   /*a EXITCODE of 0.  It can be rerun as often as*/
   /*required to check the status of the C:D process.*/
   /*When the C:D process finishes, complete the job*/
   /*to release the successors.*/
   AGENT CYBA_ISBFP3
   USER cduser
   STMPTM= WOBDATA('PARMSET.!ESPAPPL','STMPTM')
   PID= WOBDATA('PARMSET.!ESPAPPL','PID')
   CMDNAME /apps/condir/bfsbin/cdstat.sh
   ARGS !STMPTM  j!PID
   RUN WEEKDAYS
ENDJOB

UNIX_JOB ARZSDWTYI03_CLMS_TO_JP
   AGENT CYBA_ISBFP3
   USER bfpadm
   SCRIPTNAME /local/bin/arcfile.sh
   ARGS interfaces/output/dataout/zsdwtyi03_claims_to_jp.txt D
   RUN WEEKDAYS
   RELEASE ADD(LIE.!ESPAPPL)
ENDJOB

JOB LIE.!ESPAPPL LINK PROCESS
   RUN DAILY
ENDJOB

