APPL MMM020

INVOKE 'SWS.ESP.APPL.PROCLIB(#DISTRIB)'

/*********************************************************************/
/*CRI-286 Consignment items report extract
/*********************************************************************/

NOTIFY FAILURE ABEND ALERT(REMD)
NOTIFY ABEND FAILURE OVERDUE MAILBOX(AJKRONJOBREQ)

DATA_OBJECT PARMSET.!ESPAPPL
   /*Create a timestamp in hhmmMMDDYY format*/
   SETVAR TMSTMP='!ESPSHH.:!ESPSMN.,!ESPSMM./!ESPSDD./!ESPSYY'
   SETVAR PID=''
   SETVAR STMPTM='!ESPSMM./!ESPSDD./!ESPSYY.,!ESPSHH.:!ESPSMN'
ENDJOB

JOB LIS.!ESPAPPL LINK PROCESS
    RUN LAST DAY OF MONTH
    DELAYSUB 01.10
    RELEASE ADD(ZMMIMR25_MONTHLY_CONSIGNMENT_EXT)
ENDJOB

JOB SETPID.!ESPAPPL LINK PROCESS
    /*get the next PID number from the ConnectDirect*/
    /*Process ID table (CDPROD).  Save the value into*/
    /*the applications DataObject for use by the C:D*/
    /*job and the CDSTAT job (if needed).*/
    VINCR (PID) TABLE(CDPROD) CLANG
    VGET  (PID) TABLE(CDPROD) CLANG
    ESP MGRMSG * . . . PARMSET.!ESPAPPL/!ESPAPPL..!ESPAPGEN/MAIN +
ACTION SET PID(!PID)
    RELEASE ADD(SAPTOMROSS_CONSIGNCD)
    RUN LAST DAY OF MONTH
 ENDJOB

SAP_JOB ZMMIMR25_MONTHLY_CONSIGNMENT_EXT
    SAPUSER SAPADM
    AGENT CYBB_BFP030
    SAPJOBNAME ZMMIMR25_MONTHLY_CONSIGNMENT_EXT
    SAPJOBCLASS C
    ABAPNAME ZMMIMR25
    VARIANT RSH-CONS
    LANGUAGE E
    PRINTDEST CC16
    PRINTCOPIES 001
    EXPIRATION 8
    COLUMNS 170
    BANNER D
    RECIPIENT SAPADM
    ARCMODE PRINT
    RUN LAST DAY OF MONTH
    RELEASE ADD(SAPTOMROSS_CONSIGNCD)
ENDJOB

UNIX_JOB SAPTOMROSS_CONSIGNCD
    AGENT CYBA_ISBFP3
    USER cdprod
    EARLYSUB REALNOW PLUS 20 SECONDS
    PID= WOBDATA('PARMSET.!ESPAPPL','PID')
    TMSTMP= WOBDATA('PARMSET.!ESPAPPL','TMSTMP')
/*@@ "/apps/condir/bfsbin/submit.sh saptomross_consign.cd 5"
    SCRIPTNAME /apps/condir/bfsbin/submitcd.sh
    ARGS saptomross_consign.cd 5 !PID !TMSTMP
    RESOURCE ADD(1,ISBFP3CD)
    RUN LAST DAY OF MONTH
    RELEASE ADD(CDSTAT.SAPMRS03(A))
    RELEASE ADD(RMMIMR25.ISBFP3)
ENDJOB

 AIX_JOB CDSTAT.SAPMRS03 CONDITIONAL
    /*This job only runs if the C:D job does not end with*/
    /*a EXITCODE of 0.  It can be rerun as often as*/
    /*required to check the status of the C:D process.*/
    /*When the C:D process finishes, complete the job*/
    /*to release the successors.*/
    AGENT CYBA_ISBFP3
    USER cduser
    STMPTM= WOBDATA('PARMSET.!ESPAPPL','STMPTM')
    PID= WOBDATA('PARMSET.!ESPAPPL','PID')
    CMDNAME /apps/condir/bfsbin/cdstat.sh
    ARGS !STMPTM  j!PID
    RUN LAST DAY OF MONTH
 ENDJOB

UNIX_JOB RMMIMR25.ISBFP3
    AGENT CYBA_ISBFP3
    USER cdprod
    CMDNAME /bin/rm
    ARGS /data/BFP/interfaces/output/dataout/zmmimr25_consignment.txt
    RUN LAST DAY OF MONTH
    RELEASE ADD(LIE.!ESPAPPL)
ENDJOB

JOB LIE.!ESPAPPL LINK PROCESS
    RUN LAST DAY OF MONTH
ENDJOB

