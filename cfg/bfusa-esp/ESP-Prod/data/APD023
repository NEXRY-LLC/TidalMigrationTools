APPL APD023 WAIT

INVOKE 'SWS.ESP.APPL.PROCLIB(#DISTRIB)'

/*********************************************************************/
/*Documentum Valid Invoice Extract*/
/*03/18/2019 -JFC-  UPDATED PROC PER  INC000001878221                */
/*********************************************************************/

NOTIFY ABEND FAILURE ALERT(REMD)

DATA_OBJECT PARMSET.APD023
   /*Create a timestamp in hhmmMMDDYY format*/
   SETVAR TMSTMP='!ESPSHH.:!ESPSMN.,!ESPSMM./!ESPSDD./!ESPSYY'
   SETVAR PID=''
   SETVAR STMPTM='!ESPSMM./!ESPSDD./!ESPSYY.,!ESPSHH.:!ESPSMN'
ENDJOB

JOB LIS.!ESPAPPL LINK PROCESS
   RUN DAILY
   DELAYSUB 05.30
   RELEASE ADD(ZFIAPI17_DOCUMENTUM_VALID_INV)
ENDJOB

SAP_JOB ZFIAPI17_DOCUMENTUM_VALID_INV
   AGENT CYBB_BFP030
   SAPJOBNAME ZFIAPI17_DOCUMENTUM_VALID_INV
   SAPJOBCLASS C
   STARTMODE ASAP
   ABAPNAME ZFIAPI17
   VARIANT VALIDINV_WKLY
   STEPUSER SAPADM
   LANGUAGE E
   PRINTREL YES
   EXPIRATION 8
   LINES 65
   COLUMNS 132
   BANNER D
   RECIPIENT MOOREKAREN
   ARCMODE PRINT
   PRINTCOPIES 001
   RUN DAILY
   RELEASE ADD(ZFIAPI17_VALID_INVOICESCD)
ENDJOB

JOB SETPID.APD023 LINK PROCESS
   /*get the next PID number from the ConnectDirect*/
   /*Process ID table (CDPROD).  Save the value into*/
   /*the applications DataObject for use by the C:D*/
   /*job and the CDSTAT job (if needed).*/
   VINCR (PID) TABLE(CDPROD) CLANG
   VGET  (PID) TABLE(CDPROD) CLANG
   ESP MGRMSG * . . . PARMSET.!ESPAPPL/!ESPAPPL..!ESPAPGEN/MAIN +
ACTION SET PID(!PID)
   RUN DAILY
   RELEASE ADD(ZFIAPI17_VALID_INVOICESCD)
ENDJOB

UNIX_JOB ZFIAPI17_VALID_INVOICESCD
   AGENT CYBA_ISBFP3
   EARLYSUB REALNOW PLUS 20 SECONDS
   PID= WOBDATA('PARMSET.!ESPAPPL','PID')
   TMSTMP= WOBDATA('PARMSET.!ESPAPPL','TMSTMP')
   SCRIPTNAME /apps/condir/bfsbin/submitcd.sh
   ARGS zfiapi17_valid_invoices.cd 5 !PID !TMSTMP
   RESOURCE ADD(1,ISBFP3CD)
   USER cdprod
   RUN DAILY
   RELEASE ADD(CDSTAT.ZFIAPI17(A))
  RELEASE ADD(LIE.!ESPAPPL)
ENDJOB

AIX_JOB CDSTAT.ZFIAPI17 CONDITIONAL
   AGENT CYBA_ISBFP3
   /*This job only runs if the C:D job does not end with*/
   /*a EXITCODE of 0.  It can be rerun as often as*/
   /*required to check the status of the C:D process.*/
   /*When the C:D process finishes, complete the job*/
   /*to release the successors.*/
   USER cduser
   STMPTM= WOBDATA('PARMSET.!ESPAPPL','STMPTM')
   PID= WOBDATA('PARMSET.!ESPAPPL','PID')
   CMDNAME /apps/condir/bfsbin/cdstat.sh
   ARGS !STMPTM  j!PID
   RUN DAILY
ENDJOB

JOB LIE.!ESPAPPL LINK PROCESS
  RUN DAILY
ENDJOB
