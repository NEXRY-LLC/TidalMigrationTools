
APPL RSUSR100

INVOKE 'SWS.ESP.APPL.PROCLIB(#DISTRIB)'



DATA_OBJECT PARMSET.!ESPAPPL
  /*Create a timestamp in hhmmMMDDYY format*/
  SETVAR TMSTMP='!ESPSHH.:!ESPSMN.,!ESPSMM./!ESPSDD./!ESPSYY'
  SETVAR PID=''
  SETVAR STMPTM='!ESPSMM./!ESPSDD./!ESPSYY.,!ESPSHH.:!ESPSMN'
ENDJOB

JOB LIS.!ESPAPPL LINK PROCESS
  RUN TODAY
  RELEASE ADD(Z_RSUSR100_BFP)
ENDJOB

SAP_JOB Z_RSUSR100_BFP
  SAPUSER SAPADM
  AGENT CYBB_BFP030
  SAPJOBNAME Z_RSUSR100_BFP
  SAPJOBCLASS C
  ABAPNAME RSUSR100
  VARIANT Z_SAPALLNEW_CD
  LANGUAGE E
  PRINTDEST CP21
  PRINTCOPIES 001
  EXPIRATION 8
  COLUMNS 132
  BANNER D
  RECIPIENT SAPADM
  ARCMODE PRINT
  RUN TODAY
  RELDELAY 1
  RELEASE ADD(RSUSR100.ISBFP3)
ENDJOB

JOB SETPID.!ESPAPPL LINK PROCESS
  /*get the next PID number from the ConnectDirect*/
  /*Process ID table (CDPROD).  Save the value into*/
  /*the applications DataObject for use by the C:D*/
  /*job and the CDSTAT job (if needed).*/
  VINCR (PID) TABLE(CDPROD) CLANG
  VGET  (PID) TABLE(CDPROD) CLANG
  ESP MGRMSG * . . . PARMSET.!ESPAPPL/!ESPAPPL..!ESPAPGEN/MAIN +
  ACTION SET PID(!PID)
  RUN TODAY
  RELEASE ADD(RSUSR100.ISBFP3)
ENDJOB

UNIX_JOB RSUSR100.ISBFP3
  AGENT CYBA_ISBFP3
  USER cdprod
  EARLYSUB REALNOW PLUS 20 SECONDS
  PID= WOBDATA('PARMSET.!ESPAPPL','PID')
  TMSTMP= WOBDATA('PARMSET.!ESPAPPL','TMSTMP')
  SCRIPTNAME /apps/condir/bfsbin/submitcd.sh
  ARGS rsusr100.cd 5 !PID !TMSTMP
  RESOURCE ADD(1,ISBFP3CD)
  RUN TODAY
  RELEASE ADD(CDSTAT.RSUSR100(A))
ENDJOB

AIX_JOB CDSTAT.RSUSR100    CONDITIONAL
  /*This job only runs if the C:D job does not end with*/
  /*a EXITCODE of 0.  It can be rerun as often as*/
  /*required to check the status of the C:D process.*/
  /*When the C:D process finishes, complete the job*/
  /*to release the successors.*/
  AGENT CYBA_ISBFP3
  USER cduser
  STMPTM= WOBDATA('PARMSET.!ESPAPPL','STMPTM')
  PID= WOBDATA('PARMSET.!ESPAPPL','PID')
  CMDNAME /apps/condir/bfsbin/cdstat.sh
  ARGS !STMPTM  j!PID
  RUN TODAY
ENDJOB


