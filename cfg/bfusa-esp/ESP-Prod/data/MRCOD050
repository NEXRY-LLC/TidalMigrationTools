APPL MRCOD050 WAIT

INVOKE 'SWS.ESP.APPL.PROCLIB(#DISTRIB)'

/**********************************************************************/
/*CR-038 Product Cost Report For MONTERREY                            */
/*02/13/2019 -JFC- CHANGED SAP PRINDEST PER INC000001851980         **/
/*********************************************************************/

NOTIFY ABEND FAILURE ALERT(REMD)

JOB LIS.!ESPAPPL LINK PROCESS
   RUN DAILY
   EARLYSUB 02.20
   RELEASE ADD(ZCOPCR04_SCRAP_COST_MR)
ENDJOB

DATA_OBJECT PARMSET.!ESPAPPL
   /*Create a timestamp in hhmmMMDDYY format*/
   SETVAR TMSTMP='!ESPSHH.:!ESPSMN.,!ESPSMM./!ESPSDD./!ESPSYY'
   SETVAR PID=''
   SETVAR STMPTM='!ESPSMM./!ESPSDD./!ESPSYY.,!ESPSHH.:!ESPSMN'
ENDJOB

SAP_JOB ZCOPCR04_SCRAP_COST_MR
   AGENT CYBB_BFP030
   SAPJOBNAME ZCOPCR04_SCRAP_COST_MR
   SAPJOBCLASS C
   STARTMODE ASAP
   ABAPNAME ZCOPCR04
   VARIANT '2000 PPC1SCRAP'
   STEPUSER SAPADM
   LANGUAGE E
   PRINTDEST CP21
   EXPIRATION 8
   COLUMNS 255
   BANNER D
   RECIPIENT SAPADM
   ARCMODE PRINT
   PRINTCOPIES 001
   RUN DAILY
   RELEASE ADD(SAPTOMFIN_ZCOPCR04_2000CD)
ENDJOB

JOB SETPID.!ESPAPPL LINK PROCESS
   /*get the next PID number from the ConnectDirect*/
   /*Process ID table (CDPROD).  Save the value into*/
   /*the applications DataObject for use by the C:D*/
   /*job and the CDSTAT job (if needed).*/
   VINCR (PID) TABLE(CDPROD) CLANG
   VGET  (PID) TABLE(CDPROD) CLANG
   ESP MGRMSG * . . . PARMSET.!ESPAPPL/!ESPAPPL..!ESPAPGEN/MAIN +
   ACTION SET PID(!PID)
   RUN DAILY
   RELEASE ADD(SAPTOMFIN_ZCOPCR04_2000CD)
ENDJOB

UNIX_JOB SAPTOMFIN_ZCOPCR04_2000CD
   AGENT CYBA_ISBFP3
   EARLYSUB REALNOW PLUS 20 SECONDS
   PID= WOBDATA('PARMSET.!ESPAPPL','PID')
   TMSTMP= WOBDATA('PARMSET.!ESPAPPL','TMSTMP')
   SCRIPTNAME /apps/condir/bfsbin/submitcd.sh
   ARGS saptomfin_zcopcr04_2000.cd 5  !PID !TMSTMP
   RESOURCE ADD(1,ISBFP3CD)
   USER cdprod
   RUN DAILY
   RELEASE ADD(CDSTAT.MROPCR04(A))
   RELEASE ADD(LIE.!ESPAPPL)
ENDJOB

AIX_JOB CDSTAT.MROPCR04 CONDITIONAL
   AGENT CYBA_ISBFP3
   AFTER ADD(SAPTOMFIN_ZCOPCR04_2000CD(A))
   /*This job only runs if the C:D job does not end with*/
   /*a EXITCODE of 0.  It can be rerun as often as*/
   /*required to check the status of the C:D process.*/
   /*When the C:D process finishes, complete the job*/
   /*to release the successors.*/
   USER cduser
   STMPTM= WOBDATA('PARMSET.!ESPAPPL','STMPTM')
   PID= WOBDATA('PARMSET.!ESPAPPL','PID')
   CMDNAME /apps/condir/bfsbin/cdstat.sh
   ARGS !STMPTM  j!PID
   RUN DAILY
ENDJOB

JOB LIE.!ESPAPPL LINK PROCESS
   RUN DAILY
ENDJOB

