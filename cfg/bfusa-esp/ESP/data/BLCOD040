APPL BLCOD040 WAIT

NOTIFY FAILURE ABEND ALERT(REMD)
INVOKE 'SWS.ESP.APPL.PROCLIB(#DISTRIB)'

/*********************************************************************/
/*CR038 Product Cost Report for Bloomington
/*********************************************************************/
/*02/13/2019 -JFC- CHANGED SAP PRINDEST PER INC000001851980         **/
/*03/27/2019 -JFC- changes made per INC000001858289                  */
/*********************************************************************/

DATA_OBJECT PARMSET.!ESPAPPL
   /*Create a timestamp in hhmmMMDDYY format*/
   SETVAR TMSTMP='!ESPSHH.:!ESPSMN.,!ESPSMM./!ESPSDD./!ESPSYY'
   SETVAR PID=''
   SETVAR STMPTM='!ESPSMM./!ESPSDD./!ESPSYY.,!ESPSHH.:!ESPSMN'
ENDJOB

JOB LIS.BLCOD040 LINK PROCESS
  RUN DAILY
  DELAYSUB 01.00
  RELEASE ADD(ZCOPCR38_PRODUCT_COST_BL)
ENDJOB

SAP_JOB ZCOPCR38_PRODUCT_COST_BL
    AGENT CYBB_BFP030
    SAPJOBNAME ZCOPCR38_PRODUCT_COST_BL
    SAPJOBCLASS C
    SAPUSER SAPADM
    ABAPNAME ZCOPCR38
    VARIANT 'F111 PPC1'
    LANGUAGE E
    PRINTDEST CP21
    PRINTCOPIES 001
    EXPIRATION 8
    COLUMNS 255
    BANNER D
    RECIPIENT SAPADM
    ARCMODE PRINT
    RUN DAILY
    RELEASE ADD(SAPTOMFIN_ZCOPCR38_F111CD)
ENDJOB

JOB SETPID.!ESPAPPL LINK PROCESS
   /*get the next PID number from the ConnectDirect*/
   /*Process ID table (CDPROD).  Save the value into*/
   /*the applications DataObject for use by the C:D*/
   /*job and the CDSTAT job (if needed).*/
   VINCR (PID) TABLE(CDPROD) CLANG
   VGET  (PID) TABLE(CDPROD) CLANG
   ESP MGRMSG * . . . PARMSET.!ESPAPPL/!ESPAPPL..!ESPAPGEN/MAIN +
ACTION SET PID(!PID)
   RUN DAILY
   RELEASE ADD(SAPTOMFIN_ZCOPCR38_F111CD)
ENDJOB

UNIX_JOB SAPTOMFIN_ZCOPCR38_F111CD
    AGENT CYBA_ISBFP3
    EARLYSUB REALNOW PLUS 20 SECONDS
    PID= WOBDATA('PARMSET.!ESPAPPL','PID')
    TMSTMP= WOBDATA('PARMSET.!ESPAPPL','TMSTMP')
    SCRIPTNAME /apps/condir/bfsbin/submitcd.sh
    ARGS saptomfin_zcopcr38_f111.cd 5 !PID !TMSTMP
    RESOURCE ADD(1,ISBFP3CD)
    USER cdprod
    RUN DAILY
    RELEASE ADD(CDSTAT.BLOPCR38(A))
    RELEASE ADD(LIE.BLCOD040)
ENDJOB

 AIX_JOB CDSTAT.BLOPCR38     CONDITIONAL
    AGENT CYBA_ISBFP3
    /*This job only runs if the C:D job does not end with*/
    /*a EXITCODE of 0.  It can be rerun as often as*/
    /*required to check the status of the C:D process.*/
    /*When the C:D process finishes, complete the job*/
    /*to release the successors.*/
    USER cduser
    STMPTM= WOBDATA('PARMSET.!ESPAPPL','STMPTM')
    PID= WOBDATA('PARMSET.!ESPAPPL','PID')
    CMDNAME /apps/condir/bfsbin/cdstat.sh
    ARGS !STMPTM  j!PID
    RUN DAILY
 ENDJOB

JOB LIE.BLCOD040 LINK PROCESS
    RUN DAILY
ENDJOB

