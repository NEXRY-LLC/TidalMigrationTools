APPL TE25LYN2 WAIT

INVOKE 'SWS.ESP.APPL.PROCLIB(#DISTRIB)'

/*********************************************************************/
/*02/01/2017-JFC-ADDED STATEMENT TO TRIGGER EVENT TE9424U PER J. HICKS
/*********************************************************************/

NOTIFY FAILURE ABEND ALERT(REMD)

DATA_OBJECT PARMSET.!ESPAPPL
   /*Create a timestamp in hhmmMMDDYY format*/
   SETVAR TMSTMP='!ESPSHH.:!ESPSMN.,!ESPSMM./!ESPSDD./!ESPSYY'
   SETVAR PID=''
   SETVAR STMPTM='!ESPSMM./!ESPSDD./!ESPSYY.,!ESPSHH.:!ESPSMN'
ENDJOB

JOB LIS.TE25LYN2 LINK PROCESS
   /*CALENDAR=COMPANY*/
   RUN 1ST WORKDAY OF MONTH
   EARLYSUB 04.30
   RELEASE ADD(TE25LYN2.INBOUND)
ENDJOB

FILE_TRIGGER TE25LYN2.INBOUND
   AGENT CYBA_ISHRP1
   FILENAME '/data/HRP/interfaces/output/dataout/+
Payrollsap.txt' CREATE NOCHANGE(1)
   RELEASE ADD(TE25LYN2.CD)
   RUN 1ST WORKDAY OF MONTH
ENDJOB

JOB SETPID1.!ESPAPPL LINK PROCESS
   /*GET THE NEXT PID NUMBER FROM THE CONNECTDIRECT*/
   /*PROCESS ID TABLE (CDPROD).  SAVE THE VALUE INTO*/
   /*THE APPLICATIONS DATAOBJECT FOR USE BY THE C:D*/
   /*JOB AND THE CDSTAT JOB (IF NEEDED).*/
   VINCR (PID) TABLE(CDPROD) CLANG
   VGET  (PID) TABLE(CDPROD) CLANG
   ESP MGRMSG * . . . PARMSET.!ESPAPPL/!ESPAPPL..!ESPAPGEN/MAIN +
ACTION SET PID(!PID)
   RUN 1ST WORKDAY OF MONTH
   RELEASE ADD(TE25LYN2.CD)
ENDJOB

UNIX_JOB TE25LYN2.CD
   AGENT CYBA_ISHRP1
   EARLYSUB REALNOW PLUS 10 SECONDS
   PID= WOBDATA('PARMSET.!ESPAPPL','PID')
   TMSTMP= WOBDATA('PARMSET.!ESPAPPL','TMSTMP')
   /*@@ "/apps/condir/bfsbin/submitcd.sh TE25LYN2.cd 5"*/
   SCRIPTNAME /apps/condir/bfsbin/submitcd.sh
   ARGS TE25LYN2.cd 10 !PID !TMSTMP
   USER cdhrprd
   RELDELAY  1
   RUN 1ST WORKDAY OF MONTH
   RELEASE ADD(ARCHIVE.TE25LYN2_LYNCHVAL)
ENDJOB

AIX_JOB ARCHIVE.TE25LYN2_LYNCHVAL
   AGENT CYBA_ISHRP1
   SCRIPTNAME /local/bin/arcfile.sh
   ARGS interfaces/output/dataout/Payrollsap.txt M
   USER hrpadm
   RUN 1ST WORKDAY OF MONTH
   RELEASE ADD(LIE.TE25LYN2)
ENDJOB

AIX_JOB CDSTAT1.!ESPAPPL CONDITIONAL
   AGENT CYBA_ISHRP1
   /*This job only runs if the C:D job does not end with*/
   /*a EXITCODE of 0.  It can be rerun as often as*/
   /*required to check the status of the C:D process.*/
   /*When the C:D process finishes, complete the job*/
   /*to release the successors.*/
   USER cduser
   STMPTM= WOBDATA('PARMSET.!ESPAPPL','STMPTM')
   PID= WOBDATA('PARMSET.!ESPAPPL','PID')
   CMDNAME /apps/condir/bfsbin/cdstat.sh
   ARGS !STMPTM  j!PID
   AFTER ADD(TE25LYN2.CD(A))
   RUN 1ST WORKDAY OF MONTH
ENDJOB

JOB LIE.TE25LYN2 LINK PROCESS
  RUN FIRST DAY OF MONTH
  ESPNOMSG TRIGGER TE01.TE9424U ADD
ENDJOB

