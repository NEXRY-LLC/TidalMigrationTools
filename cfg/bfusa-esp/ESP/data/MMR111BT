APPL MMR111BT

INVOKE 'SWS.ESP.APPL.PROCLIB(#DISTRIB)'

/*********************************************************************/
/*05/09/2018 -JFC- CHANGED EVENT PREFIX FROM OF01
/*09/19/2018 -JFC- ADDED 1 MIN DEALY TO SAP JOB
/*02/13/2019 -JFC- CHANGED SAP PRINDEST PER INC000001851980         **/
/*********************************************************************/

NOTIFY FAILURE ABEND ALERT(REMD)

DATA_OBJECT PARMSET.!ESPAPPL
  /*Create a timestamp in hhmmMMDDYY format*/
  SETVAR TMSTMP='!ESPSHH.:!ESPSMN.,!ESPSMM./!ESPSDD./!ESPSYY'
  SETVAR PID=''
  SETVAR STMPTM='!ESPSMM./!ESPSDD./!ESPSYY.,!ESPSHH.:!ESPSMN'
ENDJOB

JOB LIS.!ESPAPPL LINK PROCESS
  RUN TODAY
  RELEASE ADD(ZMMIMI25_IMDS_BIT_TEAM_V2)
ENDJOB

SAP_JOB ZMMIMI25_IMDS_BIT_TEAM_V2
  SAPUSER SAPADM
  AGENT CYBB_BFP030
  SAPJOBNAME ZMMIMI25_IMDS_BIT_TEAM_V2
  SAPJOBCLASS C
  ABAPNAME ZMMIMI25
  VARIANT 'BIT TEAM_V2'
  LANGUAGE E
  PRINTDEST CP21
  PRINTCOPIES 001
  EXPIRATION 8
  COLUMNS 132
  BANNER D
  RECIPIENT SAPADM
  ARCMODE PRINT
  RUN TODAY
  NOTIFY FAILURE EVENT(SW01.RESUBJOB)
  NOTIFY ABEND FAILURE ALERT(REMD)
  RELDELAY 1
  RELEASE ADD(ZMMIMI25.ISBFP3)
ENDJOB

JOB SETPID.!ESPAPPL LINK PROCESS
  /*get the next PID number from the ConnectDirect*/
  /*Process ID table (CDPROD).  Save the value into*/
  /*the applications DataObject for use by the C:D*/
  /*job and the CDSTAT job (if needed).*/
  VINCR (PID) TABLE(CDPROD) CLANG
  VGET  (PID) TABLE(CDPROD) CLANG
  ESP MGRMSG * . . . PARMSET.!ESPAPPL/!ESPAPPL..!ESPAPGEN/MAIN +
  ACTION SET PID(!PID)
  RUN TODAY
  RELEASE ADD(ZMMIMI25.ISBFP3)
ENDJOB

UNIX_JOB ZMMIMI25.ISBFP3
  AGENT CYBA_ISBFP3
  USER cdprod
  EARLYSUB REALNOW PLUS 20 SECONDS
  PID= WOBDATA('PARMSET.!ESPAPPL','PID')
  TMSTMP= WOBDATA('PARMSET.!ESPAPPL','TMSTMP')
  SCRIPTNAME /apps/condir/bfsbin/submitcd.sh
  ARGS zmmimi25_temporary.cd 5 !PID !TMSTMP
  RESOURCE ADD(1,ISBFP3CD)
  RUN TODAY
  RELEASE ADD(CDSTAT.ZMMIMI25(A))
  RELEASE ADD(ARZMMIMI25)
ENDJOB

AIX_JOB CDSTAT.ZMMIMI25     CONDITIONAL
  /*This job only runs if the C:D job does not end with*/
  /*a EXITCODE of 0.  It can be rerun as often as*/
  /*required to check the status of the C:D process.*/
  /*When the C:D process finishes, complete the job*/
  /*to release the successors.*/
  AGENT CYBA_ISBFP3
  USER cduser
  STMPTM= WOBDATA('PARMSET.!ESPAPPL','STMPTM')
  PID= WOBDATA('PARMSET.!ESPAPPL','PID')
  CMDNAME /apps/condir/bfsbin/cdstat.sh
  ARGS !STMPTM  j!PID
  RUN TODAY
ENDJOB

UNIX_JOB ARZMMIMI25
  AGENT CYBA_ISBFP3
  USER bfpadm
  SCRIPTNAME /local/bin/arcfile.sh
  ARGS interfaces/output/dataout/zmmimi25_iod_boms D
  RUN TODAY
  RELEASE ADD(LIE.!ESPAPPL)
ENDJOB

JOB LIE.!ESPAPPL LINK PROCESS
  RUN TODAY
ENDJOB

