APPL PED110C WAIT

INVOKE 'SWS.ESP.APPL.PROCLIB(#DISTRIB)'

/*********************************************************************/
/*INT-128  -  Material Characteristics Extract
/*YMIMCHAR(YMIMCHAR_MIMI_MMP CHAR EXT TB)  Extract data from SAP+
           for use in MIMI
/*MTCHARCD (saptomimi_matchar_tb.cd) Move the data from SAP to Appm
/*THE MIMI CD JOBS IN THIS APPL COMMUNICATE WITH APPM-NA-CHQ         */
/*********************************************************************/

NOTIFY FAILURE ABEND ALERT(REMD)

AIX_JOB LIE.LVPED100 EXTERNAL APPLID(LVPED100)
  RUN DAILY
  RELEASE ADD(LIS.!ESPAPPL)
ENDJOB

JOB LIS.!ESPAPPL LINK PROCESS
  RUN DAILY
  RELEASE ADD(YMIMCHAR_MIMI_MMP_CHAR_EXT_TB)
ENDJOB

SAP_JOB YMIMCHAR_MIMI_MMP_CHAR_EXT_TB                                   XT_TB')
    SAPUSER SAPADM
    AGENT CYBB_BFP030
    SAPJOBNAME 'YMIMCHAR_MIMI MMP CHAR EXT TB'
    SAPJOBCLASS A
    ABAPNAME YMIMCHAR
    VARIANT 'VARIANT TB'
    LANGUAGE E
    PRINTCOPIES 001
    EXPIRATION 8
    LINES 65
    COLUMNS 80
    BANNER D
    RECIPIENT DUETTAD
    ARCMODE PRINT
    RUN DAILY
    RELEASE ADD(SAPTOMIMI_MATCHAR_TBCD)
ENDJOB

DATA_OBJECT PARMSET.!ESPAPPL
   /*Create a timestamp in hhmmMMDDYY format*/
   SETVAR TMSTMP='!ESPSHH.:!ESPSMN.,!ESPSMM./!ESPSDD./!ESPSYY'
   SETVAR PID=''
   SETVAR STMPTM='!ESPSMM./!ESPSDD./!ESPSYY.,!ESPSHH.:!ESPSMN'
ENDJOB

JOB SETPID.!ESPAPPL LINK PROCESS
   /*get the next PID number from the ConnectDirect*/
   /*Process ID table (CDPROD).  Save the value into*/
   /*the applications DataObject for use by the C:D*/
   /*job and the CDSTAT job (if needed).*/
   VINCR (PID) TABLE(CDPROD) CLANG
   VGET  (PID) TABLE(CDPROD) CLANG
   ESP MGRMSG * . . . PARMSET.!ESPAPPL/!ESPAPPL..!ESPAPGEN/MAIN +
   ACTION SET PID(!PID)
   RUN DAILY
   RELEASE ADD(SAPTOMIMI_MATCHAR_TBCD)
ENDJOB

UNIX_JOB SAPTOMIMI_MATCHAR_TBCD
    AGENT CYBA_ISBFP3
    EARLYSUB REALNOW PLUS 20 SECONDS
    PID= WOBDATA('PARMSET.!ESPAPPL','PID')
    TMSTMP= WOBDATA('PARMSET.!ESPAPPL','TMSTMP')
    SCRIPTNAME /apps/condir/bfsbin/submitcd.sh
    ARGS saptomimi_matchar_tb.cd 5 !PID !TMSTMP
    USER cdprod
    RUN DAILY
    RELEASE ADD(CDSTAT.MTCHARCD(A))
    RELEASE ADD(LIE.!ESPAPPL)
ENDJOB

AIX_JOB CDSTAT.MTCHARCD     CONDITIONAL
   AGENT CYBA_ISBFP3
   /*This job only runs if the C:D job does not end with*/
   /*a EXITCODE of 0.  It can be rerun as often as*/
   /*required to check the status of the C:D process.*/
   /*When the C:D process finishes, complete the job*/
   /*to release the successors.*/
   USER cduser
   STMPTM= WOBDATA('PARMSET.!ESPAPPL','STMPTM')
   PID= WOBDATA('PARMSET.!ESPAPPL','PID')
   CMDNAME /apps/condir/bfsbin/cdstat.sh
   ARGS !STMPTM  j!PID
   RUN DAILY
ENDJOB

AIX_JOB LIE.!ESPAPPL
   AGENT CYBA_ISBFP3
   COMMAND /bin/sleep
   ARGS 3
   USER bfpadm
   RUN DAILY
ENDJOB

