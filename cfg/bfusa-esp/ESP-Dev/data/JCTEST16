APPL JCTEST16 WAIT

INVOKE 'SWS.ESP.APPL.PROCLIB(#DISTRIB)'

/*********************************************************************/
/*    THIS IS A TEST OF THE ESP SCHEDULING SYSTEM                    */
/*********************************************************************/
/* TESTING FILE TRIGGERS                                             */
/* TESTING APPL END STATEMTENTS AND PARAMETERS                       */
/*********************************************************************/
 /*****************************************************/
 /*****************************************************/
 /*- TEST OF MVS JOB THAT WILL RETURN ANY CC FROM 0 - 16  */
 /*- JUST ENTER THE CODE YOU WANT THE JOB TO END WITH
 /*- INTO USER 1 PARAMETER FIELD WHEN TRIGGEREING THE EVENT
 /*****************************************************/

NOTIFY FAILURE ABEND END OVERDUE MAILBOX(COSTELLOJOHN)
CRITPATH ON
TAG TEST

FILE_TRIGGER FILE_MONITOR_TRIGGER_APPL_UPON_UPDATE
  AGENT APPCYB02PPD
  USER maestro
  FILENAME \\APPCYB02PPD\ESP$\EDIT_2_RUN_APPL_JCTEST03.txt UPDATE
/*RUN WEEKDAYS
  NORUN JAN 16 2034
  DUEOUT EXEC REALNOW PLUS 102 HOURS
  RELEASE ADD(MERRY_CHRISTMAS)
ENDJOB

JOB MERRY_CHRISTMAS LINK PROCESS
  RUN DEC 25
  RELEASE ADD(HAPPY_NEW_YEAR_2)
ENDJOB

JOB HAPPY_NEW_YEAR_2 LINK PROCESS
  RUN JAN 1
  RELEASE ADD(GOOD_MORNING)
ENDJOB

JOB GOOD_MORNING LINK PROCESS
  DELAYSUB 1
  INVOKE 'SWS.ESP.APPL.TESTLIB(#JCRUND8)'
  RUN TODAY
  RELEASE ADD(JCTESTCC)
ENDJOB

JOB USERREQ.READY_TO_CONTINUE TASK
  RUN TODAY
  NOTIFY JOBSTART MAILBOX(COSTELLOJOHN) +
   SUBJECT('MANUAL TASK IN APPL JCTEST16 NEEDS COMPLETED')
  RELEASE ADD(JCTESTCC)
ENDJOB

JOB JCTESTCC
  DATASET 'FTRF.TSTJOBS'
  CCCHK RC(1:3) FAIL CONTINUE
  CCCHK RC(5:11) FAIL CONTINUE
  CCCHK RC(13:4095) FAIL CONTINUE
  RUN TODAY
  /*@@ RUN JOB RT950C72 FROM PROC61 ON DATES BELOW */
   IF TODAY('NOV 23 2016') THEN DATASET 'FTRF.SPRJOBS'
   IF TODAY('NOV 24 2016') THEN DATASET 'FTRF.SPRJOBS'
  /*@@ RUN JOB RT950C72 FROM PROC61 ON DATES ABOVE */
    IF ESPSHH = '16' THEN ABANDON DEPENDENCIES 4.30PM
  RELEASE ADD(JCTEST00) COND(RC(0))
  RELEASE ADD(JCTEST04) COND(RC(4))
ENDJOB

JOB JCTEST00 LINK PROCESS
  CCCHK RC(1:4095) FAIL CONTINUE
  RUN TODAY
  RELEASE ADD(JCTEST00.2) COND(RC(0))
ENDJOB

JOB JCTEST00.2 LINK PROCESS
  CCCHK RC(1:4095) FAIL CONTINUE
  RUN TODAY
  RELEASE ADD(LIE.JCTEST02)
ENDJOB

JOB JCTEST04 LINK PROCESS
  CCCHK RC(1:4095) FAIL CONTINUE
  RUN TODAY
  PREREQ (JCTESTCC) COND(RC(04))
  RELEASE ADD(JCTEST04.2)
ENDJOB

JOB JCTEST04.2 LINK PROCESS
  CCCHK RC(1:4095) FAIL CONTINUE
  RUN TODAY
  RELEASE ADD(LIE.JCTEST02)
ENDJOB

JOB JCTEST12 LINK PROCESS
  CCCHK RC(1:4095) FAIL CONTINUE
  RUN TODAY
  PREREQ (JCTESTCC) COND(RC(12))
  RELEASE ADD(JCTEST12.2)
ENDJOB

JOB JCTEST12.2 LINK PROCESS
  CCCHK RC(1:4095) FAIL CONTINUE
  RUN TODAY
  PREREQ (JCTEST12) COND(RC(0))
/*RELEASE ADD(LIE.JCTEST02)
ENDJOB

JOB LIE.JCTEST02 LINK PROCESS hold
  RUN TODAY
  RELEASE ADD(BACK.2.MAIN.FRAME)
ENDJOB

JOB BACK.2.MAIN.FRAME request
  MEMBER USIEBR14
  RUN TODAY
  IF TODAY('OCT 12 2015') THEN RUN TODAY
  RELEASE ADD(GO_GET_SOME_COFFEE)
ENDJOB

JOB GO_GET_SOME_COFFEE LINK PROCESS hold
  TAG TESTJOB
  RUN MON
  RUN TODAY
  RELEASE ADD(RUN_COMMAND_ON_AIX_BOX)
ENDJOB

AIX_JOB RUN_COMMAND_ON_AIX_BOX CRITICAL
  AGENT CYBA_ISBFP3
  COMMAND /bin/sleep
  ARGS 3
  USER bfpadm
  RUN DAILY
  RELEASE ADD(RUN_COMMAND_ON_WINDOWS_SERVER)
ENDJOB

NT_JOB RUN_COMMAND_ON_WINDOWS_SERVER
  AGENT CYBA_PPIWPROD
  CMDNAME X:\cmd_scripts\LinkedInEnd.cmd
  ARGS
  USER maestro
  RUN DAILY
  RELEASE ADD(EMAIL.SUPPORT.PROCESS.COMPLETE)
ENDJOB

JOB EMAIL.SUPPORT.PROCESS.COMPLETE LINK PROCESS
  RUN TODAY
ENDJOB

APPLEND JCTEST03
  ESPNOMSG TRIGGER !ESPEVENT ADD
  RUN WEEKDAYS SAT
ENDJOB


